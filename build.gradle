import io.gitlab.arturbosch.detekt.Detekt

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        // Project
        agp_version = '7.0.2'
        kotlin_version = '1.5.21'
        diktat_version = '1.0.0-rc.3'
        detekt_version = '1.18.1'
        jacoco_tool_version = '0.8.7'
        jacoco_android_version = '0.1.5-SNAPSHOT'

        // Libs
        compose_version = '1.0.2'
        androidx_core_ktx_version = '1.6.0'
        androidx_appcompat_version = '1.3.1'
        google_android_material_version = '1.4.0'
        androidx_lifecycle_runtime_ktx_version = '2.3.1'
        androidx_activity_compose_version = '1.3.1'
        androidx_navigation_compose_version = '2.4.0-alpha08'
        coil_compose_version = '1.3.2'
        kotlin_serialization_json_version = '1.3.0'
        ktor_version = '1.6.4'
        coroutines_version = '1.5.2'
        retrofit_version = '2.9.0'

        // Test
        junit_version = '4.13.2'
        androidx_test_junit_ext_version = '1.1.3'
        androidx_test_espresso_core_version = '3.4.0'
    }
    repositories {
        google()
        mavenCentral()
        maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$agp_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.cqfn.diktat:diktat-gradle-plugin:$diktat_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
        classpath "com.dicedmelon.gradle:jacoco-android:$jacoco_android_version"
    }
}

apply plugin: "org.cqfn.diktat.diktat-gradle-plugin"
apply plugin: "io.gitlab.arturbosch.detekt"

diktat {
    inputs = files("**/src/**/*.kt")
}

detekt {
    reports {
        html.enabled = true // observe findings in your browser with structure and code snippets
        txt.enabled = true // similar to the console output, contains issue signature to manually edit baseline files
    }
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes += ['jdk.internal.*']
}

tasks.withType(Detekt).configureEach {
    jvmTarget = "1.8"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}